<html>
  <head>
    <script type="application/javascript" src="base.js"></script>
    <script id="l0-functions" type="x-shader/x-vertex">
      const float kEpsilon = 1e-0;

      vec3 Constant(float c) {
        return vec3(0.0, 0.0, c);
      }

      vec3 Multiply(vec3 u, vec3 v) {
        return vec3(
          u.x * v.z + u.z * v.x,
          u.y * v.z + u.z * v.y,
          u.z * v.z
        );
      }

      vec3 Divide(vec3 u, vec3 v) {
        return vec3(
          (u.x * v.z - u.z * v.x) / v.z / v.z,
          (u.y * v.z - u.z * v.y) / v.z / v.z,
          u.z / v.z
        );
      }

      vec3 Sin(vec3 u) {
        return vec3(
          u.x * cos(u.z),
          u.y * cos(u.z),
          sin(u.z)
        );
      }

      vec3 X(float x) {
        return vec3(1.0, 0.0, x);
      }

      vec3 Y(float y) {
        return vec3(0.0, 1.0, y);
      }

      vec3 Sinc(vec3 u) {
        return Divide(
          Sin(u - Constant(10.0 * time) + Constant(kEpsilon)),
          u + Constant(kEpsilon)
        );
      }

      vec3 Square(vec3 u) {
        return vec3(
          u.x * 2.0 * u.z,
          u.y * 2.0 * u.z,
          u.z * u.z
        );
      }

      vec3 SquareRoot(vec3 u) {
        float s = sqrt(u.z);
        return vec3(u.x / s / 2.0, u.y / s / 2.0, s);
      }

      vec3 f(vec2 xy);

      vec3 f(vec2 xy) {
        vec3 x = X(xy.x);
        vec3 y = Y(xy.y);
        vec3 t = Constant(time);
        // Separate f into additive components, (a, b, c), to workaround a weird NVIDIA bug.
        vec3 a = (
          Multiply(
            Sinc(
              Divide(
                Square(x) + Square(y),
                Constant(100.0)
              )
            ),
            Constant(10.0)
          )
        );
        vec3 b = (
          Multiply(
            Constant(20.0),
            Sin(
              Divide(
                x,
                Constant(40.0)
              ) - (
                t
              )
            )
          )
        );
        vec3 c = (
          Divide(
            Square(x) + Square(y),
            Constant(3900.0)
          )
        );
        return vec3(a.x + b.x + c.x, a.y + b.y + c.y, a.z + b.z + c.z);
      }

      vec3 normal_f(vec2 xz) {
        vec3 f = f(xz);
        return normalize(vec3(-f.x, 1.0, -f.y));
      }
    </script>
    <script id="v0-preamble" type="x-shader/x-vertex">
      uniform float time;
      uniform mat4 projection, rotation, rotation_inverse, translation, translation_inverse;
      attribute vec3 position, barycentric;
      varying vec3 fragment_barycentric, fragment_position;
      varying vec4 fragment_world_position;
    </script>
    <script id="v0-main" type="x-shader/x-fragment">
      void main() {
        mat4 transform = rotation * translation;
        mat4 transform_inverse = translation_inverse * rotation_inverse;
        fragment_barycentric = barycentric;
        vec4 p = translation_inverse * vec4(position, 1.0);
        p /= p.w;
        vec4 zero = translation_inverse * vec4(vec3(0.0), 1.0);
        zero /= zero.w;
        float f_at_0 = f(zero.xz).z;
        float f_at_xz = f(p.xz).z;
        fragment_position = vec3(p.x, f_at_xz/* - f_at_0*/, p.z);
        fragment_world_position = transform * vec4(fragment_position, 1.0);
        gl_Position = projection * fragment_world_position;
      }
    </script>
    <script id="f0-preamble" type="x-shader/x-fragment">
      precision mediump float;
      uniform bool wireframe;
      uniform highp float time;
      uniform highp mat4 rotation, rotation_inverse, translation, translation_inverse;
      uniform vec3 particle, sgd, momentum, nesterov, adagrad, adadelta;
      uniform vec3 particle_color, sgd_color, momentum_color, nesterov_color, adagrad_color, adadelta_color;
      varying vec3 fragment_barycentric, fragment_normal, fragment_position;
      varying vec4 fragment_world_position;
    </script>
    <script id="f0-main" type="x-shader/x-fragment">
      vec3 solver_position(vec3 position) {
        return vec3(position.x, f(position.xz).z, position.z);
      }

      vec3 light(vec3 normal, vec3 color, vec3 position, float offset) {
        vec3 light_vector = position - fragment_position.xyz;
        vec3 light_direction = normalize(light_vector.xyz);
        float light_square_distance = dot(light_vector.xyz, light_vector.xyz);
        float product = dot(normal, light_direction);
        product = clamp(product, 0.0, 1.0);
        float light = 1.0 * (1.0e2 * product / light_square_distance +
            5.0e1 * product / sqrt(light_square_distance));
        return light * color;
      }

      vec3 sun(vec3 normal, vec3 color) {
        vec3 sun_direction = normalize(vec3(-1.0, 1.0, 1.0));
        float product = dot(normal, sun_direction);
        product *= 0.5;
        product += 0.5;
        product *= product;
        return product * color;
      }

      void main() {
        vec3 normal = normal_f(fragment_position.xz);
        vec3 particle_light = light(normal, particle_color, particle, 1.0);
        vec3 sgd_light = light(normal, sgd_color, solver_position(sgd), 1.0);
        vec3 momentum_light = light(normal, momentum_color, solver_position(momentum), 1.0);
        vec3 nesterov_light = light(normal, nesterov_color, solver_position(nesterov), 1.0);
        vec3 adagrad_light = light(normal, adagrad_color, solver_position(adagrad), 1.0);
        vec3 adadelta_light = light(normal, adadelta_color, solver_position(adadelta), 1.0);
        vec3 sunlight = sun(normal, vec3(0.83));
        vec3 all_light = sunlight + particle_light + sgd_light + momentum_light + nesterov_light + adagrad_light + adadelta_light;
        if (gl_FrontFacing) {
          if (wireframe && any(lessThan(fragment_barycentric.xz, vec2(0.05)))) {
            gl_FragColor = vec4(vec3(0.0), 1.0);
          } else {
            gl_FragColor = vec4(all_light, 1.0);
          }
        } else {
          gl_FragColor = vec4(all_light, 0.5);
          // discard;
        }
      }
    </script>
    <script id="v1-main" type="x-shader/x-vertex">
      uniform float radius;
      uniform mat4 projection, rotation, rotation_inverse, translation, translation_inverse;
      uniform vec3 particle_position;
      attribute vec3 angles, barycentric;
      varying vec3 fragment_barycentric;
      const float M_PI = 3.14159265358979323846;
      void main() {
        mat4 transform = rotation * translation;
        vec3 angles1 = angles - vec3(M_PI / 2.0, 0.0, 0.0);
        vec3 position = radius * vec3(
            sin(angles1.x) * cos(2.0 * angles1.z),
            cos(angles1.x) + 1.0,
            sin(angles1.x) * sin(2.0 * angles1.z));
        gl_Position = projection * transform * vec4(position + particle_position, 1.0);
        fragment_barycentric = barycentric;
      }
    </script>
    <script id="f1-main" type="x-shader/x-fragment">
      precision mediump float;
      uniform vec3 color;
      varying vec3 fragment_barycentric;

      void main() {
        // if (any(lessThan(fragment_barycentric, vec3(0.05)))) {
        //   gl_FragColor = vec4(vec3(0.0), 1.0);
        // } else {
          gl_FragColor = vec4(color, 1.0);
        // }
      }
    </script>
    <link href="http://fonts.googleapis.com/css?family=Ubuntu+Mono:400,700,400italic,700italic"
        rel="stylesheet" type="text/css">
    <style type="text/css">
      body {
        margin: 0px;
      }
      #c {
        width: 100%;
        height: 100%;
      }
      #container {
        float: left;
      }
      #l0-editor {
        font-family: 'Ubuntu Mono';
        white-space: pre;
        padding-left: 1em;
      }
      #scoreboard {
        position: absolute;
        padding-left: 1em;
        z-index: 1;
        top: 0;
        left: 0;
        background: transparent;
        font-family: 'Ubuntu Mono';
        font-weight: bold;
      }
      .particle {
        color: rgb(255, 0, 0);
      }
      .sgd {
        color: rgb(0, 0, 255);
      }
      .momentum {
        color: rgb(0, 255, 0);
      }
      .nesterov {
        color: rgb(255, 0, 255);
      }
      .adagrad {
        color: rgb(255, 255, 0);
      }
      .adadelta {
        color: rgb(0, 255, 255);
      }
    </style>
  </head>
  <body onload="bouncingball.load();">
    <div id="container">
      <canvas id='c0'></canvas>
    </div>
    <div id="scoreboard">
      <p class="particle">Particle: <span id="particle" class="particle"></span></p>
      <p class="sgd">Stochastic Gradient Descent: <span id="sgd" class="sgd"></span></p>
      <p class="momentum">Momentum: <span id="momentum" class="momentum"></span></p>
      <p class="nesterov">Nesterov: <span id="nesterov" class="nesterov"></span></p>
      <p class="adagrad">Adagrad: <span id="adagrad" class="adagrad"></span></p>
      <p class="adadelta">Adadelta: <span id="adadelta" class="adadelta"></span></p>
    </div>
  </body>
</html>
